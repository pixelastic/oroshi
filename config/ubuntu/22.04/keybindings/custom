#!/usr/bin/env zsh
set -e

# Note that some keybindings can't be defined at the Ubuntu level, they seem to
# be intercepted before (for example <Super>I). For those, we use xbindkeys
# instead
local GSETTINGS_ROOT="org.gnome.settings-daemon.plugins.media-keys.custom-keybinding"
local GSETTINGS_DIRECTORY="/org/gnome/settings-daemon/plugins/media-keys/custom-keybindings"

local                  shortcuts=(
# Name                 Command                                               Binding
"Calculator"           "gnome-calculator"                                    "Insert"
"Chrome"               "google-chrome"                                       "<Super>C"
"Flameshot"            "/usr/bin/flameshot gui"                              "<Super>Print"
"Keepass"              "keepassx ~/perso/Dropbox/tim/keys.kdbx"              "<Super>K"
"Kitty"                "/usr/bin/kitty"                                      "<Super>T"
"Notion"               "notion-snap"                                         "<Super>N"
"Peek"                 "/home/tim/.oroshi/scripts/bin/peek"                  "<Super><Alt>Print"
"Pomodoro Start/Pause" "/home/tim/.oroshi/scripts/bin/pomodoro-toggle-pause" "<Super><Alt>I"
"Pomodoro Start/Stop"  "/home/tim/.oroshi/scripts/bin/pomodoro-toggle-stop"  "<Super><Alt>O"
"Slack"                "/usr/bin/slack"                                      "<Super>S"
"Snippet Pixie"        "snippetpixie --search-and-paste"                                        "<Super>P"
)

function gsettings_set {
  local path=$1
  local key=$2
  local value=$3
  # echo "gsettings set $path $key $value"
  /usr/bin/gsettings set "$path" "$key" "$value"
}


# Iterating through all the bindings
local shortcut_count=$((${#shortcuts[@]} / 3))
local shortcut_inventory=()
for shortcut_index in {1..$shortcut_count}; do
  local custom_index=$((shortcut_index - 1))
  local array_index=$((1 + $custom_index * 3))

  local shortcut_name=$shortcuts[$array_index]
  local shortcut_command=$shortcuts[$array_index+1]
  local shortcut_binding=$shortcuts[$array_index+2]

  # Setting each shortcut individually
  gsettings_set "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "name" "'${shortcut_name}'"
  gsettings_set "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "command" "'${shortcut_command}'"
  gsettings_set "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "binding" "'${shortcut_binding}'"

  shortcut_inventory+="'${GSETTINGS_DIRECTORY}/custom${custom_index}/'"
done

gsettings_set "org.gnome.settings-daemon.plugins.media-keys" "custom-keybindings" "[${(j:,:)shortcut_inventory}]"


