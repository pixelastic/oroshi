#compdef

# Suggest list of all subcommands of yarn run
# - scripts defined in package.json
# - local binaries installed in node_modules
#
# Note:
# _describe is a helper function that, it seems, should only be called once in
# a completion function. Calling it twice groups the headers at the top instead
# of adding them between completion groups.
#
# To work around that here, we will display the results of the second group
# (binaries) using the low-level compadd. compadd works well when you need to
# simply add elements. To also pass descriptions, it requires a bit more work
# (passing two arrays, one with suggestions and one with descriptions), so this
# is why we stick with the higher level _describe here for the first group, and
# the low-level compadd for the second one.
function _yarn-runnables() {
	# package.json scripts
	local scriptSuggestions=(${(f)"$(complete-yarn-scripts)"})
	if [[ $scriptSuggestions != "" ]]; then
		_describe -V "$(completion-header $COLOR_ALIAS_LANGUAGE_JAVASCRIPT $COLOR_BLACK '  package.json scripts ')" scriptSuggestions
	fi

	# local node_modules/.bin binaries
	local binarySuggestions=(${(f)"$(complete-yarn-binaries)"})
	if [[ $binarySuggestions != "" ]]; then
		compadd \
			-J group2 \
			-x "$(completion-header $COLOR_ALIAS_LANGUAGE_JAVASCRIPT $COLOR_BLACK '  node_modules binaries ')" \
			-a binarySuggestions
	fi
}
