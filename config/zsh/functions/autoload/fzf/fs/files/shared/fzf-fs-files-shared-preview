# Preview a file inside fzf
# Usage
# $ fzf-fs-files-shared-preview ./path/to/file                  # Display the file
# $ fzf-fs-files-shared-preview ./path/to/folder                # Display the folder
# $ fzf-fs-files-shared-preview ./file --highlight-line 42      # Highlight the specified line
# $ fzf-fs-files-shared-preview ./file --highlight-word "query" # Highlight the specified query
function fzf-fs-files-shared-preview() {
  # Parse arguments
  zmodload zsh/zutil
  zparseopts -E -D \
    -highlight-line:=flagHighlightLine \
    -highlight-query:=flagHighlightQuery \

  # Get full path of file to preview
  local fullPath="$1"
  fullPath=${~fullPath}

  # Cleanup previously displayed image
  image-display --clear

  # Directory {{{
  if [[ -d $fullPath ]]; then
    l "$fullPath"
    return 0
  fi
  # }}}

  local filetypeGroup="$(filetype-group $fullPath)"
  local extension=${fullPath:e}

  # Image display
  if [[ $filetypeGroup == "image" ]]; then
    image-display --fzf-preview $fullPath
    return 0
  fi

  # PDF display
  if [[ $extension == "pdf" ]]; then
    local pdfCoverCache="/tmp/oroshi/fzf/pdf-covers"
    local pdfCoverHash=$(file-hash $fullPath);
    local pdfCoverPath="${pdfCoverCache}/${pdfCoverHash}.png"

    # Create the cover if none exist
    if [[ ! -f $pdfCoverPath ]]; then
      mkdir -p $pdfCoverCache
      convert "${fullPath}[0]" $pdfCoverPath
      png-trim $pdfCoverPath
    fi

    image-display --no-metadata --fzf-preview $pdfCoverPath
    return 0
  fi

  # Text preview
  local highlightLine=${flagHighlightLine[2]}
  local highlightQuery=${flagHighlightQuery[2]}

  local lineRangeMin=0
  local lineRangeMax=50
  local contextSize=4

  # If a specific line is passed, we highlight it and the context around only
  if [[ $highlightLine != "" ]]; then
    local lineRangeMin=$(($highlightLine - $contextSize))
    [[ $lineRangeMin -lt 0 ]] && lineRangeMin=0
    local lineRangeMax=$(($highlightLine + $contextSize))
    local highlightFlag="--highlight-line $highlightLine"
  fi

  # Display file
  local output="$(bat \
    --paging=never \
    --style=numbers \
    --color=always \
    ${=highlightFlag} \
    --line-range $lineRangeMin:$lineRangeMax \
    $fullPath
  )"

  # If no query passed, we display it like this
  if [[ $highlightQuery == "" ]]; then
    echo $output
    return 0
  fi

  # We highlight the query
  rg \
    --no-line-number \
    "$highlightQuery" \
    <<< "$output"
}

