# Displays a colorized version of a branch name
# Usage:
# $ git-branch-colorize                         # {currentBranch}
# $ git-branch-colorize main                    # main
# $ git-branch-colorize --with-icon             #  main
# $ git-branch-colorize origin/develop --remote #  origin/remote
function git-branch-colorize() {
  zparseopts -E -D \
    -with-icon=flagWithIcon \
    -remote=flagRemote
  local isWithIcon=${#flagWithIcon}
  local isRemote=${#flagRemote}

  # Branch name {{{
  local branchName="$1"
  # Default to current branch if none specified
  if [[ "$branchName" == '' ]]; then
    branchName="$(git-branch-current)"
  fi
  branchName=${branchName:s#/dependabot/npm_and_yarn#/  }
  # }}}
  # Branch color {{{
  if [[ "$isRemote" == "1" ]]; then
    local branchColor=$COLOR_ALIAS_GIT_REMOTE
  else
    local branchColor="$(git-branch-color $branchName)"
  fi
  # }}}

  # If --with-icon is not passed, we simply display the colored branch
  if [[ "$isWithIcon" != 1 ]]; then
    colorize "$branchName" $branchColor
    return
  fi

  # If --with-icon is passed, we need to add an icon and display a specific
  # color based on the local/remote relationship

  # For remote branches, color as a remote
  if [[ $isRemote == '1' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  local branchPushStatus="$(git-branch-push-status $branchName)"

  if [[ $branchPushStatus = 'never_pushed' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'identical' ]]; then
    colorize "$branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'ahead' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'behind' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'gone' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'diverged' ]]; then
    colorize " $branchName" $branchColor
    return
  fi

  if [[ $branchPushStatus = 'detached' ]]; then
    local currentCommit="$(git-commit-current)"
    colorize " HEAD ($currentCommit)" $branchColor
    return
  fi

  # Fallback on unknown status
  colorize " $branchName" $branchColor
  return
}
