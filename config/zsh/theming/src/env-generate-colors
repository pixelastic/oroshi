#!/usr/bin/env zsh
# Regenerate the COLOR_ definition file f
#
# This will define COLORS environment variables
# - COLOR_RED_0 will be the lightest shade of red
# - COLOR_RED_9 will be the darkest shade of red
# - COLOR_RED will be equivalent to COLOR_RED_5
# - The default variables will return the color number
# - Adding _HEXA at the end will return the hexadecimal value
local output=""

# The following ranges and color number should match what is defined in kitty/colors.conf
# Named colors {{{
declare -A namedColors
namedColors=()
namedColors[0]="BLACK"
namedColors[1]="RED"
namedColors[2]="GREEN"
namedColors[3]="YELLOW"
namedColors[4]="BLUE"
namedColors[5]="PURPLE"
namedColors[6]="CYAN"
namedColors[7]="WHITE"
namedColors[8]="BLACK_PURE"
namedColors[9]="RED_LIGHT"
namedColors[10]="GREEN_LIGHT"
namedColors[11]="YELLOW_LIGHT"
namedColors[12]="BLUE_LIGHT"
namedColors[13]="PURPLE_LIGHT"
namedColors[14]="CYAN_LIGHT"
namedColors[15]="WHITE_PURE"
namedColors[16]="GRAY"
namedColors[17]="ORANGE"
namedColors[18]="EMERALD"
namedColors[19]="AMBER"
namedColors[20]="SKY"
namedColors[21]="VIOLET"
namedColors[22]="TEAL"
namedColors[23]="NEUTRAL"
namedColors[24]="GRAY_LIGHT"
namedColors[25]="ORANGE_LIGHT"
namedColors[26]="EMERALD_LIGHT"
namedColors[27]="AMBER_LIGHT"
namedColors[28]="SKY_LIGHT"
namedColors[29]="VIOLET_LIGHT"
namedColors[30]="TEAL_LIGHT"
namedColors[31]="NEUTRAL_LIGHT"
# }}}
# Color palettes {{{
local colorRanges=()
# colorRanges[5]="XXX?"
colorRanges[6]="RED"
colorRanges[7]="GREEN"
colorRanges[8]="YELLOW"
colorRanges[9]="BLUE"
colorRanges[10]="PURPLE"
colorRanges[11]="CYAN"
# colorRanges[12]="XXX?"
colorRanges[13]="GRAY"
colorRanges[14]="ORANGE"
colorRanges[15]="EMERALD"
colorRanges[16]="AMBER"
colorRanges[17]="SKY"
colorRanges[18]="VIOLET"
colorRanges[19]="TEAL"
colorRanges[20]="NEUTRAL"
# }}}
# Alias colors {{{
declare -A aliasColors
aliasColors=()
# Common
aliasColors[DIRECTORY]="GREEN"
aliasColors[EXECUTABLE]="VIOLET"
aliasColors[FILE]="VIOLET"
aliasColors[LINK]="BLUE"
aliasColors[LOCAL_DEPENDENCY]="YELLOW"
aliasColors[MATCH]="YELLOW"
aliasColors[SELECTED_BACKGROUND]="GRAY_8"
aliasColors[SELECTED_FOREGROUND]="WHITE"
aliasColors[TERMINAL]="BLACK_PURE"
aliasColors[UI]="NEUTRAL"
aliasColors[UNKNOWN]="CYAN_4"
# Languages
aliasColors[LANGUAGE_JAVASCRIPT]="YELLOW"
aliasColors[LANGUAGE_RUBY]="ORANGE"
# States
aliasColors[ERROR]="RED"
aliasColors[SUCCESS]="GREEN"
aliasColors[WARNING]="YELLOW"
aliasColors[NOTICE]="VIOLET"
# Git
aliasColors[GIT_COMMIT]="AMBER_6"
aliasColors[GIT_MESSAGE]="GRAY"
aliasColors[GIT_STASH]="TEAL_3"
aliasColors[GIT_REBASE]="RED_LIGHT"
aliasColors[GIT_ADDED]="GREEN"
aliasColors[GIT_MODIFIED]="VIOLET"
aliasColors[GIT_DIRTY]="RED"
aliasColors[GIT_REMOTE]="YELLOW"
aliasColors[GIT_REMOVED]="RED"
aliasColors[GIT_PULLREQUEST]="PURPLE"
aliasColors[GIT_ISSUE]="YELLOW"
# Code
aliasColors[EVAL]="YELLOW"                      # - in pug
aliasColors[BOOLEAN]="AMBER_5"
aliasColors[COMMENT]="GRAY"
aliasColors[CONSTANT]="ORANGE"
aliasColors[DATE]="SKY_9"
aliasColors[EXCEPTION]="RED"
aliasColors[FLAG]="AMBER"                       # -h, --help
aliasColors[FUNCTION]="YELLOW"
aliasColors[GLOB]="GREEN_LIGHT"
aliasColors[MODIFIER]="ORANGE_LIGHT"                    # :hover
aliasColors[HEADER]="YELLOW"
aliasColors[IMPORT]="YELLOW_LIGHT"
aliasColors[INTERPOLATION_STRING]="BLUE"        # The wrapping string
aliasColors[INTERPOLATION_VARIABLE]="VIOLET"    # The variable name
aliasColors[INTERPOLATION_WRAPPER]="YELLOW"     # The ${} delimiter
aliasColors[KEYWORD]="GREEN"
aliasColors[NOISE]="TEAL_7"
aliasColors[NUMBER]="BLUE"                      # Bold
aliasColors[PUNCTUATION]="TEAL"
aliasColors[REGEXP]="BLUE"
aliasColors[SPECIAL_CHAR]="AMBER"               # Escape sequences, keybindings
aliasColors[STATEMENT]="GREEN"                  # for, if, while
aliasColors[STRING]="BLUE"
aliasColors[SYMBOL]="ORANGE"                    # :symbol in ruby
aliasColors[TEXT]="GRAY_LIGHT"
aliasColors[TODO]="RED"
aliasColors[VARIABLE]="VIOLET"                  # Reading it
aliasColors[VARIABLE_DEFINITION]="VIOLET_LIGHT" # Left-hand side, writing it
aliasColors[VARIABLE_TYPE]="RED_LIGHT"          # const, let, function
# }}}

# This will also create a COLORS_INDEX env variable, with all the color names
local colorsIndex=()

# We define all ENV variables for all colors
local kittyColors="$(cat ~/.oroshi/config/kitty/colors.conf | grep '^color')"
for line in ${(f)kittyColors}; do
  # Full line: color105  #6b7280
  local split=(${=line})
  # Term value: 105
  local colorTermValue=${split[1]:5}
  # Hexadecimal value
  local colorHexaValue=$split[2]

  # Known color
  if [[ $namedColors[$colorTermValue] != "" ]]; then
    local colorKey="$namedColors[$colorTermValue]"
    [[ "$colorKey" == "" ]] && continue
  else
    # Palette color
    # Range start: 10 (GRAY)
    local colorRangeValue=${colorTermValue:0:-1}
    [[ $colorRangeValue == "" ]] && continue
    local colorRangeName=$colorRanges[$colorRangeValue]
    [[ $colorRangeName == "" ]] && continue

    # Scale index: 5
    local colorScaleValue=$colorTermValue[-1]

    local colorKey="${colorRangeName}_${colorScaleValue}"
  fi

  output+="export COLOR_${colorKey}=\"${colorTermValue}\"\n"
  output+="export COLOR_${colorKey}_HEXA=\"${colorHexaValue}\"\n"

  colorsIndex+=(${colorKey})
done

# Adding all color aliases
for aliasName colorName in "${(@kv)aliasColors}"; do
  output+="export COLOR_ALIAS_${aliasName}=\$COLOR_${colorName}\n"
  output+="export COLOR_ALIAS_${aliasName}_HEXA=\$COLOR_${colorName}_HEXA\n"
done

# Alphabetical list of all colors
local COLORS_INDEX=""
for colorName in ${(o)colorsIndex}; do
  COLORS_INDEX+=" $colorName"
done
for colorName in ${(ko)aliasColors}; do
  COLORS_INDEX+=" ALIAS_${colorName}"
done
output+="export COLORS_INDEX=\"${COLORS_INDEX:1}\"\n"


# Generate the file
echo $output > ~/.oroshi/config/zsh/theming/env/colors.zsh

