function fzf-fs-directories-common-source() {
  # We load z
  source ~/local/etc/zsh-z/zsh-z.plugin.zsh

  # We get the sorted list of the most used folders
  local rawList="$(
  z -l \
    | tac \
    | cut -d ' ' -f 1 --complement \
    | awk '{$1=$1;print}' \
  )"

  # We iterate on each path, to potentially color them using the project prefix
  for fullPath in ${(f)rawList}; do
    local displayPath="$fullPath/"
    local projectKey="$(project-by-path ${displayPath})"

    # Update display if known project
    if [[ $projectKey != "" ]]; then
      local projectPath=${(P)${:-PROJECT_${projectKey}_PATH}}
      eval "displayPath=\${displayPath:s_${projectPath}_}"

      local projectPrefix="$(project-colorize $projectKey)"

      displayPath="${projectPrefix} ${displayPath}"
    fi

    echo "$fullPath   ${displayPath}"
  done
}
