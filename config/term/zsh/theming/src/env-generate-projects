#!/usr/bin/env zsh
# Creating PROJECT entries, for example:
# - PROJECT_ABERLAAS_NAME
# - PROJECT_ABERLAAS_PATH
# - PROJECT_ABERLAAS_ICON
# - PROJECT_ABERLAAS_BACKGROUND
# - PROJECT_ABERLAAS_BACKGROUND_NAME
# - PROJECT_ABERLAAS_BACKGROUND_HEXA
# - PROJECT_ABERLAAS_BACKGROUND_INACTIVE (dark version for inactive tabs)
# - PROJECT_ABERLAAS_BACKGROUND_INACTIVE_NAME
# - PROJECT_ABERLAAS_BACKGROUND_INACTIVE_HEXA
# - PROJECT_ABERLAAS_FOREGROUND
# - PROJECT_ABERLAAS_FOREGROUND_NAME
# - PROJECT_ABERLAAS_FOREGROUND_HEXA
# - PROJECT_ABERLAAS_HIDE_NAME_IN_PROMPT
local output=""
local outputFile=$ZSH_CONFIG_PATH/theming/env/projects.zsh

# This will load PROJECTS definitions. For example:
#
# - PROJECTS[aberlaas:background]="YELLOW_7"
# - PROJECTS[aberlaas:icon]="î‰£ "
# - PROJECTS[aberlaas:path]="~/local/www/projects/aberlaas/"
# - PROJECTS[aberlaas:foreground]="GRAY_9"
source $ZSH_CONFIG_PATH/theming/src/projects-list.zsh

# We'll keep a list of all projects, and a list of all paths
# - PROJECTS_INDEX will contains an alphabetical list of all projects
# - PROJECTS_INDEX_BY_PATH will contains the same list, ordered by path
# For now, we gather them in an array, and we'll sort them one we have them
# all
local projectsIndex=()
declare -gA projectsIndexByPath
projectsIndexByPath=()

function addToOutput() {
  local key=$1
  local value=$2
  if [[ $value == '' ]]; then
    return
  fi

  output+="export ${key}=\"${value}\"\n"
}


# Export PROJECT_{TYPE}_{KEY} environment variables:
for key value in "${(@kv)PROJECTS}"; do
  [[ $key != *:icon ]] && continue

  # Finding the values
  # Project name: aberlaas
  local projectName=${key%:*}
  # Capitalized project name: ABERLAAS
  # Remove special characters: pixelastic.workers.dev => PIXELASTIC_WORKERS_DEV
  local projectKey=${(U)projectName:gs/-/_/}
  projectKey=${(U)projectKey:gs/\./_/}
  # Background colors
  local backgroundColorName=$PROJECTS[${projectName}:background]
  local backgroundColorTerm=${(P)${:-COLOR_${backgroundColorName}}}
  local backgroundColorHexa=${(P)${:-COLOR_${backgroundColorName}_HEXA}}
  # Background inactive colors (dark version)
  # Strip number suffix (GREEN_8 -> GREEN) and prepend DARK_
  local backgroundColorBase=${backgroundColorName%%_*}
  if [[ $backgroundColorBase != "" ]]; then
    local backgroundColorInactiveName="DARK_${backgroundColorBase}"
    local backgroundColorInactiveTerm=${(P)${:-COLOR_${backgroundColorInactiveName}}}
    local backgroundColorInactiveHexa=${(P)${:-COLOR_${backgroundColorInactiveName}_HEXA}}
  fi
  # Foreground colors
  local foregroundColorName=$PROJECTS[${projectName}:foreground]
  local foregroundColorTerm=${(P)${:-COLOR_${foregroundColorName}}}
  local foregroundColorHexa=${(P)${:-COLOR_${foregroundColorName}_HEXA}}
  # Icon
  local icon=$PROJECTS[${projectName}:icon]
  # Path
  local projectPath=$PROJECTS[${projectName}:path]
  # Should hide the name in prompt?
  local hideNameInPrompt=$PROJECTS[${projectName}:hideNameInPrompt]
  [[ $hideNameInPrompt == "" ]] && hideNameInPrompt="0"

  # Creating PROJECT entries
  addToOutput "PROJECT_${projectKey}_NAME" "$projectName"
  addToOutput "PROJECT_${projectKey}_PATH" "$projectPath"
  addToOutput "PROJECT_${projectKey}_ICON" "$icon"
  addToOutput "PROJECT_${projectKey}_BACKGROUND" "$backgroundColorTerm"
  addToOutput "PROJECT_${projectKey}_BACKGROUND_NAME" "$backgroundColorName"
  addToOutput "PROJECT_${projectKey}_BACKGROUND_HEXA" "$backgroundColorHexa"
  addToOutput "PROJECT_${projectKey}_BACKGROUND_INACTIVE" "$backgroundColorInactiveTerm"
  addToOutput "PROJECT_${projectKey}_BACKGROUND_INACTIVE_NAME" "$backgroundColorInactiveName"
  addToOutput "PROJECT_${projectKey}_BACKGROUND_INACTIVE_HEXA" "$backgroundColorInactiveHexa"
  addToOutput "PROJECT_${projectKey}_FOREGROUND" "$foregroundColorTerm"
  addToOutput "PROJECT_${projectKey}_FOREGROUND_NAME" "$foregroundColorName"
  addToOutput "PROJECT_${projectKey}_FOREGROUND_HEXA" "$foregroundColorHexa"
  addToOutput "PROJECT_${projectKey}_HIDE_NAME_IN_PROMPT" "$hideNameInPrompt"

  # Building the indices
  projectsIndex+=($projectKey)
  if [[ $projectPath != "" ]]; then
    projectsIndexByPath[$projectPath]=$projectKey
  fi
done

# Alphabetical list of all projects
local PROJECTS_INDEX=""
for projectKey in ${(o)projectsIndex}; do
  PROJECTS_INDEX+=" $projectKey"
done
addToOutput "PROJECTS_INDEX" "${PROJECTS_INDEX:1}"

# List of all projects, ordered by path, from specific to generic
local PROJECTS_INDEX_BY_PATH=""
for projectPath in ${(kO)projectsIndexByPath}; do
  local projectKey=$projectsIndexByPath[$projectPath]
  PROJECTS_INDEX_BY_PATH+=" $projectKey"
done
addToOutput "PROJECTS_INDEX_BY_PATH" "${PROJECTS_INDEX_BY_PATH:1}"

# Generate the file
echo $output > $outputFile

# Convert the output to json
zsh2json $outputFile
