#!/usr/bin/env zsh
# Configure custom global keybinding
# Those open specific applications when pressed

local ROOT_KEY="/org/gnome/settings-daemon/plugins/media-keys/custom-keybindings"

# Define keybindings {{{
local                  shortcuts=(
  # Name                 Binding           Command
  "Calculator"              "Insert"          "gnome-calculator"
  "Emote"                   "<Super>E"        "flatpak run com.tomjwatson.Emote"
  "Chrome"                  "<Super>C"        "google-chrome"
  "Firefox"                 "<Super>F"        "firefox"
  "Slack"                   "<Super>S"        "slack"
  "Kitty"                   "<Super>T"        "/home/tim/.oroshi/scripts/bin/kitty/kitty-restore"
  "Keepass"                 "<Super>K"        "/home/tim/.oroshi/scripts/bin/better-keepass /home/tim/perso/Dropbox/tim/keys.kdbx"

  "current-date"            "<Super>D"        "/home/tim/.oroshi/scripts/bin/keybindings/insert-current-date"
  "uuid"                    "<Super>U"        "/home/tim/.oroshi/scripts/bin/keybindings/insert-uuid"

  "Spotify Pause"           "<Super>I"        "/home/tim/.oroshi/scripts/bin/spotify/spotify"
  "Spotify Next"            "<Super>L"        "/home/tim/.oroshi/scripts/bin/spotify/spotify-next"
  "Spotify Previous"        "<Super>H"        "/home/tim/.oroshi/scripts/bin/spotify/spotify-previous"

  "Flameshot"               "<Super>F14"      'script --command "flameshot gui" /dev/null'
  "mic2text"                "<Ctrl>F14"       "/home/tim/.oroshi/scripts/bin/audio/mic2txt"
  "mic2text-model-toggle"   "<Shift>F14"      "/home/tim/.oroshi/scripts/bin/audio/mic2txt-model-toggle"
)
# }}}

# Reset all keybindings {{{
function resetAllKeybindings() {
  for shortcut_index in {1..20}; do
    local custom_index=$((shortcut_index - 1))
    dconf reset $ROOT_KEY/custom${custom_index}/name
    dconf reset $ROOT_KEY/custom${custom_index}/binding
    dconf reset $ROOT_KEY/custom${custom_index}/command
  done
  dconf reset $ROOT_KEY
}
resetAllKeybindings
# }}}

# Apply new bindings {{{
local shortcut_count=$((${#shortcuts[@]} / 3))
local shortcut_inventory=()
for shortcut_index in {1..$shortcut_count}; do
  local custom_index=$((shortcut_index - 1))
  local array_index=$((1 + $custom_index * 3))

  local shortcut_name=$shortcuts[$array_index]
  local shortcut_binding=$shortcuts[$array_index+1]
  local shortcut_command=$shortcuts[$array_index+2]

  # Setting each shortcut individually
  dconf write $ROOT_KEY/custom${custom_index}/name "'${shortcut_name}'"
  dconf write $ROOT_KEY/custom${custom_index}/binding "'${shortcut_binding}'"
  dconf write $ROOT_KEY/custom${custom_index}/command "'${shortcut_command}'"

  shortcut_inventory+=("'${ROOT_KEY}/custom${custom_index}/'")
done
dconf write $ROOT_KEY "[${(j:,:)shortcut_inventory}]"
# }}}
