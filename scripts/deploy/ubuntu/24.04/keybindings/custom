#!/usr/bin/env zsh
# Configure custom global keybinding
# Those open specific applications when pressed

local                  shortcuts=(
  # Name                 Command                                                  Binding
  "Calculator"           "gnome-calculator"                                       "Insert"
  "Chrome"               "google-chrome"                                          "<Super>C"
  "Firefox"              "firefox"                                                "<Super>F"
  "Kitty"                "/home/tim/.oroshi/scripts/bin/kitty/kitty-restore"      "<Super>T"
  "Keepass"              "keepassxc /home/tim/perso/Dropbox/tim/keys.kdbx"        "<Super>K"
  "Spotify Pause"        "/home/tim/.oroshi/scripts/bin/spotify/spotify"          "<Super>I"
  "Spotify Next"         "/home/tim/.oroshi/scripts/bin/spotify/spotify-next"     "<Super>L"
  "Spotify Previous"     "/home/tim/.oroshi/scripts/bin/spotify/spotify-previous" "<Super>H"
  # "Flameshot"            "/usr/bin/flameshot gui"                              "<Super>Print"
  # "Peek"                 "/home/tim/.oroshi/scripts/bin/peek"                  "<Super><Alt>Print"
  # "Slack"                "/usr/bin/slack"                                      "<Super>S"

)

local GSETTINGS_ROOT="org.gnome.settings-daemon.plugins.media-keys.custom-keybinding"
local GSETTINGS_DIRECTORY="/org/gnome/settings-daemon/plugins/media-keys/custom-keybindings"

function setKeybinding() {
  local bindingPath=$1
  local bindingKey=$2
  local bindingValue=$3
  gsettings set "$bindingPath" "$bindingKey" "$bindingValue"
}

# Iterating through all the bindings
local shortcut_count=$((${#shortcuts[@]} / 3))
local shortcut_inventory=()
for shortcut_index in {1..$shortcut_count}; do
  local custom_index=$((shortcut_index - 1))
  local array_index=$((1 + $custom_index * 3))

  local shortcut_name=$shortcuts[$array_index]
  local shortcut_command=$shortcuts[$array_index+1]
  local shortcut_binding=$shortcuts[$array_index+2]

  # Setting each shortcut individually
  setKeybinding "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "name" "'${shortcut_name}'"
  setKeybinding "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "command" "'${shortcut_command}'"
  setKeybinding "${GSETTINGS_ROOT}:${GSETTINGS_DIRECTORY}/custom${custom_index}/" "binding" "'${shortcut_binding}'"

  shortcut_inventory+=("'${GSETTINGS_DIRECTORY}/custom${custom_index}/'")
done

setKeybinding "org.gnome.settings-daemon.plugins.media-keys" "custom-keybindings" "[${(j:,:)shortcut_inventory}]"
