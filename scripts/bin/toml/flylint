#!/usr/bin/env zsh
# flylint: Lint fly.toml files
setopt extendedglob

local filepath=$(realpath "$1")

# Run fly config validate and capture output
local rawOutput=$(fly config validate --strict --config "$filepath" 2>&1)
local exitCode=$?

# Remove ANSI color codes for easier parsing
local cleanOutput=$(echo "$rawOutput" | remove-ansi)

# Split output into blocks using rare UTF-8 delimiter
cleanOutput=${cleanOutput//(#b)(WARN|WARNING|Error)/▮${match[1]}}
local blocks=(${(@s:▮:)cleanOutput})

# Process each warning block
for block in "${blocks[@]}"; do
  local message=$(trim "$block") # Trim start and end
  message=${message//[$'\n\r']/ } # Replace new lines with spaces

  # Skip if doesn't start with WARN, WARNING, or Error
  [[ ! "$message" =~ ^(WARN|WARNING|Error) ]] && continue

  # Determine message type
  local messageType="warning"
  [[ "$message" =~ ^Error ]] && messageType="error"

  # Remove prefix automatically (WARN, WARNING, or Error followed by optional colon and space)
  message=${message#*(WARN|WARNING|Error)*( |: )}

  # Format: filename:line:column:type:message
  echo "$filepath:1:1:$messageType:$message"
done

exit $exitCode
