#!/usr/bin/env zsh
# flylint: Lint fly.toml files
setopt extendedglob

local filepath=$(realpath "$1")

# Run fly config validate and capture output
local rawOutput=$(fly config validate --strict --config "$filepath" 2>&1)
local exitCode=$?

# Cleanup the output:
# - Remove ANSI characters
# - Remove "✔ Configuration is valid" line
# - Remove code, starting with line numbers
local cleanOutput=$(echo "$rawOutput" |
  remove-ansi |
  grep -v '^✓' |
  grep -v '^.\{1,3\}|')

# Split output into blocks
cleanOutput=${cleanOutput//(#b)(WARN|WARNING|Error)/▮${match[1]}}
local blocks=(${(@s:▮:)cleanOutput})

# Process each warning block
for block in "${blocks[@]}"; do
  local message=$(trim "$block") # Trim start and end
  message=${message//[$'\n\r']/ } # Replace new lines with spaces

  # Skip if doesn't start with WARN, WARNING, or Error
  [[ ! "$message" =~ ^(WARN|WARNING|Error) ]] && continue

  # Determine message type
  local messageType="warning"
  [[ "$message" =~ ^Error ]] && messageType="error"

  # Remove prefix automatically (WARN, WARNING, or Error followed by optional colon and space)
  message=${message#*(WARN|WARNING|Error)*( |: )}

  # Extract line and column if present (e.g., "row 9 column 7")
  local lineNumber=1
  local columnNumber=1
  if [[ "$message" =~ "row ([0-9]+) column ([0-9]+)" ]]; then
    lineNumber=${match[1]}
    columnNumber=${match[2]}
    # Remove the "row X column Y" part from message
    message=${message//row [0-9]# column [0-9]#/}
  fi

  message=$(trim "$message")
  # Format: filename:line:column:type:message
  echo "$filepath:$lineNumber:$columnNumber:$messageType:$message"
done

exit $exitCode
