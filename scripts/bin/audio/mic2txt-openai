#!/usr/bin/env zsh
# mic2text: Transcribe what is being said into text
# Usage:
# $ mic2txt # Starts recording then...
#   mic2txt # ...stops the recording and display the transcription

local tmpFolder=/dev/shm/oroshi/mic2txt/whisper
local pidFile=${tmpFolder}/PID
local recordFile=${tmpFolder}/record.wav

# Check if a recording is already running (so we can stop it), or not (so we can
# start it)
mkdir -p $tmpFolder
local isRecordingRunning=0
[[ -f $pidFile ]] && isRecordingRunning=1

# RECORD {{{
# No recording running, starting one
if [[ $isRecordingRunning == "0" ]]; then
  rec \
    --rate 16000 \
    --channels 1 \
    $recordFile &

  # Save the pid
  echo $! >$pidFile
  exit 0
fi
# }}}

# OUTPUT {{{
# A recording is already started, we stop it after 2s (to catch the last words)
sleep 2
kill-pid "$(cat $pidFile)"

function saveToHistory() {
  local content=$1
  local now="$(date '+%Y-%m-%d-%H-%M')"
  local historyFolder=~/local/tmp/oroshi/mic2txt/history

  mkdir -p $historyFolder
  echo $content >"${historyFolder}/${now}.log"
}

# We transcribe to text (french or english)
local transcription="$(transcribe-api $recordFile)"
saveToHistory "$transcription"

# Play a sound to indicate it's being pasted
if sound-mode-is-enabled; then
  audio-play ~/.oroshi/config/audio/sounds/bg-stealth.wav &
fi

# Insert the transcription wherever I'm currently focused
focus-insert "$transcription"

# Cleanup the pid file
rm $pidFile
# }}}
