#!/usr/bin/env zsh
# Get the list of all remote tags
# Usage:
# $ git-tag-list-remote           # Display all tags of current remote
# $ git-tag-list-remote upstream  # Display all tags of upstream

local rawList="$(
  git \
    ls-remote \
    --tags \
    --quiet \
    --sort=-creatordate \
    | grep -v '\^{}$'
)"

# Stop if no tag
if [[ "$rawList" == "" ]]; then
  echo "✘ No tags found"
  exit
fi

# Loop through them all
local output=""
local currentTag="$(git-tag-current)"
for rawLine in ${(f)rawList}; do
  local row=""

  # Splitting the line into its various parts
  local split=(${=rawLine})
  local commitHash=${split[1]:0:7}
  local tagName=${split[2]:10}

  # Tag name
  row+="$(git-tag-colorize $tagName --with-icon)▮"

  # Hash
  row+="$(git-commit-colorize $commitHash --with-icon)▮"

  output+="$row\n"
done


table $output
