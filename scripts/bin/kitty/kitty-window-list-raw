#!/usr/bin/env zsh
# List all windows in a parsable format
# Usage: kitty-window-list-raw [--tab <tabId>]
# Output format: windowId▮tabId▮userVars (one per line)
# userVars format: key1=value1:key2=value2 (empty if no vars)

zparseopts -E -D \
  -tab:=flagTab
local tabId=${flagTab[2]}

# Get all windows from all tabs
local allWindows=$(kitty@ ls | jq -r '
  .[0].tabs[]
  | . as $tab
  | .windows[]
  | .user_vars as $vars
  | ($vars | to_entries | map("\(.key)=\(.value)") | join(":")) as $varsStr
  | "\(.id)▮\($tab.id)▮\($varsStr)"
')

# If no specific filter on a tab
if [[ "$tabId" == "" ]]; then
  echo "$allWindows"
  exit 0
fi

# Filter by tab if specified
for rawLine in ${(f)allWindows}; do
  # Splitting the line into its various parts {{{
  local splitLine=("${(@ps/▮/)rawLine}")
  local windowTabId="${splitLine[2]}"
  [[ "$windowTabId" != "$tabId" ]] && continue
  echo $rawLine
done
