#!/usr/bin/env zsh
# Create a new Kitty window with a given oroshi_windowName
# Usage:
# $ kitty-window-create "myWindow"                          # Create zsh window in current tab
# $ kitty-window-create "myWindow" --tab 5                  # Create in specific tab
# $ kitty-window-create "myWindow" --color "background=red" # With custom styling
# $ kitty-window-create "myWindow" --cmd "claude"           # Launch with specific command

zparseopts -E -D \
  -tab:=flagTab \
  -cmd:=flagCmd
local tabId=${flagTab[2]}
local kittyCommand=${flagCmd[2]}
[[ "$kittyCommand" == "" ]] && kittyCommand="zsh"

local windowName="$1"
local kittyAdditionalArgs=("${@:2}")

if [[ "$windowName" == "" ]]; then
  echo "You must pass a window name"
  exit 2
fi

# Build launch command
local launchArgs=(
  --type window
  --keep-focus
  --cwd current
  --var "oroshi_windowName=${windowName}"
)

# Add tab match if provided
if [[ "$tabId" != "" ]]; then
  launchArgs+=(--match "id:${tabId}")
fi

# Add any additional arguments passed to kitty@ launch
launchArgs+=("${kittyAdditionalArgs[@]}")

# Launch and return window ID
kitty@ launch "${launchArgs[@]}" ${=kittyCommand} 1>/dev/null
