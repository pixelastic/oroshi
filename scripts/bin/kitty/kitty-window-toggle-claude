#!/usr/bin/env zsh
# Toggle between current window and a dedicated Claude Code window
# Each tab has its own Claude window stored in a "Claude" parking tab
local currentWindowId=$(kitty-window-id)
local originWindowId=$(kitty-window-get-var "$currentWindowId" "oroshi_claudeOriginWindowId")

# If we're in a Claude window, get back to the calling window
if [[ "$originWindowId" != "" ]]; then
  local claudeTabId=$(kitty-tab-id "claude")
  local originWasFullscreen=$(kitty-window-get-var "$currentWindowId" "oroshi_claudeOriginWasFullscreen")

  # Move it back to the parking tab
  if [[ "$originWasFullscreen" == "0" ]]; then
    kitty-window-fullscreen-off "$originWindowId" &
  fi
  echo "move ${currentWindowId} to tab ${claudeTabId}, focus ${originWindowId}"
  kitty-window-move "$currentWindowId" "$claudeTabId"
  kitty-window-focus "$originWindowId"

  exit 0
fi

# From here, we assume we're in a normal window and want to go to Claude
local currentTabId=$(kitty-tab-id)
local claudeWindowName="claude-${currentTabId}"

# Create the claude tab if it doesn't yet exist
if ! kitty-tab-exists "claude"; then
  kitty-tab-create "claude"
fi

local claudeTabId=$(kitty-tab-id "claude")

# Create the Claude window if it doesn't yet exist
if ! kitty-window-exists "$claudeWindowName"; then
  source $ZSH_CONFIG_PATH/theming/env/colors.zsh
  kitty-window-create \
    "$claudeWindowName" \
    --tab "$claudeTabId" \
    --spacing padding=40 \
    --color background=$COLOR_DARK_AMBER_HEXA \
    --cmd "zsh -c claude"
fi

local claudeWindowId=$(kitty-window-id "$claudeWindowName")
local currentWindowIsFullscreen=$(kitty-window-is-fullscreen "$currentWindowId" && echo "1" || echo "0")

kitty-window-fullscreen-on "$currentWindowId"
kitty-window-move "$claudeWindowId" "$currentTabId"
kitty-window-set-var "$claudeWindowId" \
  "oroshi_claudeOriginWindowId=$currentWindowId" \
  "oroshi_claudeOriginWasFullscreen=$currentWindowIsFullscreen"
