#!/usr/bin/env zsh
# Toggle between current window and a dedicated Claude Code window
# Each tab has its own Claude window stored in a "Claude" parking tab
local currentWindowId=$(kitty-window-id)
local originWindowId=$(kitty-window-get-var "$currentWindowId" "oroshi_claudeOriginWindowId")

# [CALLED FROM A CLAUDE WINDOW]
if [[ "$originWindowId" != "" ]]; then
  local claudeTabId=$(kitty-tab-id "claude")
  local originTabLayout=$(kitty-window-get-var "$currentWindowId" "oroshi_claudeOriginTabLayout")

  # Revert previous layout if not stack
  if [[ "$originTabLayout" != "stack" ]]; then
    local originTabId=$(kitty-window-get-var "$currentWindowId" "oroshi_claudeOriginTabId")
    kitty-tab-layout-switch "$originTabId" "${originTabLayout}"
  fi

  # Move the Claude window back to its specific tab
  kitty-window-move "$currentWindowId" "$claudeTabId"
  # Focus on the origin window
  kitty-window-focus "$originWindowId"

  exit 0
fi

# [CALLED FROM A NORMAL WINDOW]
local currentTabId=$(kitty-tab-id)
local claudeWindowName="claude-${currentTabId}"

# Create the claude tab if it doesn't yet exist
if ! kitty-tab-exists "claude"; then
  kitty-tab-create "claude" --layout grid
fi
local claudeTabId=$(kitty-tab-id "claude")

# Create the Claude window if it doesn't yet exist
if ! kitty-window-exists "$claudeWindowName"; then
  source $ZSH_CONFIG_PATH/theming/env/colors.zsh
  kitty-window-create \
    "$claudeWindowName" \
    --tab "$claudeTabId" \
    --spacing padding=40 \
    --color background=$COLOR_DARK_AMBER_HEXA \
    --cmd "kitty-run-claude"
fi
local claudeWindowId=$(kitty-window-id "$claudeWindowName")

local currentTabLayout=$(kitty-tab-layout "$currentTabId")

# Set the current layout as stack (so we only see one window)
kitty-tab-layout-switch "$currentTabId" "stack"
# Move the Claude window here, so it's full screen
kitty-window-move "$claudeWindowId" "$currentTabId"
# Save data about the origin window, so we can go back
kitty-window-set-var "$claudeWindowId" \
  "oroshi_claudeOriginWindowId=$currentWindowId" \
  "oroshi_claudeOriginTabId=$currentTabId" \
  "oroshi_claudeOriginTabLayout=$currentTabLayout"
