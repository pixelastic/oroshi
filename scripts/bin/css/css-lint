#!/usr/bin/env zsh
# css-lint: Lint a CSS file
# Usage:
# $ css-lint ./path/to/file.css       # For CLI
# $ css-lint --json ./path/to/file.css  # For Vim
set -e

zparseopts -E -D \
  -json=flagJson
isJson=${#flagJson}

# Input file
inputFile="${1:a}"
if [[ ! -f "$inputFile" ]]; then
  echo "Error: File '$inputFile' not found" >&2
  exit 1
fi

# Project root
projectRoot=$(yarn-root ${inputFile:h} --force 2>/dev/null)

# Determine which stylelint binary to use
globalStylelintBin="$HOME/.oroshi/node_modules/.bin/stylelint"
projectStylelintBin="$projectRoot/node_modules/.bin/stylelint"
if [[ $projectRoot != "" ]] && [[ -f $projectStylelintBin ]]; then
  stylelintBin=$projectStylelintBin
else
  stylelintBin=$globalStylelintBin
fi

# Move to project root if it exists (affects stylelint resolution of plugins)
if [[ $projectRoot != "" ]]; then
  cd "$projectRoot"
fi

# Determine which config to use
globalStylelintConfig="$HOME/.oroshi/stylelint.config.js"
projectStylelintConfig="$projectRoot/stylelint.config.js"
configArg=""
if [[ $projectRoot != "" ]] && [[ -f $projectStylelintConfig ]]; then
  configArg="--config $projectStylelintConfig"
else
  configArg="--config $globalStylelintConfig"
fi

# Optional args
formatAsJson=""
if [[ $isJson -gt 0 ]]; then
  formatAsJson="-f json"
fi

$stylelintBin \
  ${=configArg} \
  ${=formatAsJson} \
  "$inputFile" 2>&1
