#!/usr/bin/env zsh
# Runs a specific image, as a wrapper around docker run
# Usage:
# $ docker-run ubuntu          # Run ubuntu
# $ docker-run --tty ubuntu    # Accepts flags
# $ docker-run ubuntu --tty    # No matter where
# $ docker-run ubuntu -- pwd   # Run pwd in the guest

zparseopts -E -D \
  -name:=flagContainerName
local containerName=${flagContainerName[2]}

local -a args=("$@")

# Image name {{{
# Guess which argument is the imageName
local imageFullName=""
local isPassingCommandArguments="0"
local guestCommands=()
local dockerArguments=()
for arg in "${args[@]}"; do
  # -- detected, anything after is going to be command arguments
  if [[ $arg == "--" ]]; then
    isPassingCommandArguments="1"
    continue
  fi

  # Saving the command arguments
  if [[ $isPassingCommandArguments == "1" ]]; then
    guestCommands+=($arg)
    continue;
  fi

  # Skip named arguments
  if [[ $arg == -* ]]; then
    dockerArguments+=($arg)
    continue
  fi

  # Stop once an argument is a valid image name
  if docker-image-exists $arg; then
    imageFullName="$arg"
    args=(${args//${imageFullName}/})
    continue
  fi

  # If it's none of the above, it's probably the value of a given argument
  dockerArguments+=($arg)
done

# echo "args: $args"
# echo "imageFullName: $imageFullName"
# echo "dockerArguments: $dockerArguments"
# echo "guestCommands: $guestCommands"

# Stop if no image name
if [[ $imageFullName == "" ]]; then
  echo "You must pass an image name"
  exit 1
fi
# }}}

# Tag name {{{
local imageSplit=(${(@s/:/)imageFullName})
imageName=$imageSplit[1]
local tagName=$imageSplit[2]
[[ $tagName == "" ]] && tagName="latest"
# }}}

# Container name {{{
# The container name is picked based on the following:
# - If --name is passed, we use it
# - We default to the same name as the image (with _ instead of :)
# - If already taken, we append a random word
if [[ $containerName == "" ]]; then
  local containerName="${imageName//\//_}_${tagName}"

  # Already exists? Append a random string if the name is already take
  if docker-container-exists $containerName; then
    if which petname > /dev/null; then
      containerName+="-$(petname --words 1)"
    else
      containerName+="-$(echo $RANDOM | md5sum | head -c 5)"
    fi
  fi
fi
# }}}

docker run \
  --rm \
  --name "$containerName" \
  "${dockerArguments[@]}" \
  "$imageFullName" \
  "${guestCommands[@]}"
